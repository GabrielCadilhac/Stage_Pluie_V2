// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#define SIZE 1024

// ====== Struct ======
struct OBB
{
    float3 center;
    float3 size;
    float4x4 rotation;
};

struct Collision
{
    float3 pos;
    float3 normal;
    int id;
};

// ====== Buffers ======
RWStructuredBuffer<float3> Velocities;
RWStructuredBuffer<float3> Positions;
RWStructuredBuffer<float> Sizes;
RWStructuredBuffer<float4> SplashPos;
RWStructuredBuffer<float3> SplashNormal;
RWStructuredBuffer<int> ObbsCollided;
RWStructuredBuffer<OBB> Obbs;

// ====== Settings ======
const uint _NumParticles;
const uint _NumObbs;
const uint _Resolution;

const float3 _Min;
const float3 _Max;
const float3 _InitialVel;
const float3 _GlobalWind;

//Generateur de nombre aleatoire
float hash12(float2 p_p)
{
    float3 p3 = frac(float3(p_p.xyx) * .1031);
    p3 += dot(p3, p3.yzx + 33.33);
    return frac((p3.x + p3.y) * p3.z);
}

uint2 pcg2d(uint2 v)
{
    v = v * 1664525u + 1013904223u;

    v.x += v.y * 1664525u;
    v.y += v.x * 1664525u;

    v = v ^ (v >> 16u);

    v.x += v.y * 1664525u;
    v.y += v.x * 1664525u;

    v = v ^ (v >> 16u);

    return v;
}

// Retourne un nombre aleatoire entre p_Min et p_Max
float range12_hash(float2 p_p, float p_Min, float p_Max)
{
    float r = hash12(p_p);
    return p_Min * (1.0 - r) + p_Max * r;
}

float range12(uint2 p_p, float p_Min, float p_Max)
{
    uint2 r = pcg2d(p_p);
    float rand = float(r.x) * float(r.y);
    return p_Min * (1.0 - rand) + p_Max * rand;
}

// Mise a jour de la vitesse et de la position
void changePosVel(uint3 p_id, float3 p_newPos)
{
    Positions[p_id.x] = p_newPos;
    float3 Vmax = float3(0., -9.65 + 10.3 * exp(-0.6 * Sizes[p_id.x]), 0.);
    
    float varPerc = 1.2;
    float randX = _GlobalWind.x + range12_hash(p_id.xz, -_GlobalWind.x * varPerc, _GlobalWind.x * varPerc);
    float randY = range12_hash(p_id.xz, Vmax.y, Vmax.y * varPerc);
    float randZ = _GlobalWind.z + range12_hash(p_id.xz, -_GlobalWind.z * varPerc, _GlobalWind.z * varPerc);
    
    //Velocities[p_id.x] = _InitialVel + float3(randX, randY, randZ);
    Velocities[p_id.x] = Vmax + float3(randX, randY, randZ);
    
}

// Function to get the column of a matrix
float3 GetColumn(float4x4 p_matrix, int p_id)
{
    return float3(p_matrix[0][p_id], p_matrix[1][p_id], p_matrix[2][p_id]);
}

// Find the largest axis to determine the faces collided and return the point on this face
float3 CollisionPoint(float3 d, float3 p, float3 ux, float3 uy, float3 uz, OBB obb)
{
    if (d.x > d.y && d.x > d.z)
        return obb.center + sign(p.x) * (obb.size.x + 0.001) * ux + p.y * uy + p.z * uz;
    else if (d.y > d.z)
        return obb.center + p.x * ux + sign(p.y) * (obb.size.y + 0.001) * uy + p.z * uz;
    return obb.center + p.x * ux + p.y * uy + sign(p.z) * (obb.size.z + 0.001) * uz;
}

// Find largest axis of d and return the vector ui associated
float3 ComputeNormal(float3 d, float3 p, float3 ux, float3 uy, float3 uz)
{
    if (d.x > d.y && d.x > d.z)
        return ux * sign(p.x);
    else if (d.y > d.z)
        return uy * sign(p.y);
    return uz * sign(p.z);
}

// Return the index of the OBB collided
bool OBBCollision(float3 p_pos, out Collision p_col)
{
    for (uint i = 0; i < _NumObbs; i++)
    {
        OBB obb = Obbs[i];
        
        float3 ux = normalize(GetColumn(obb.rotation, 0));
        float3 uy = normalize(GetColumn(obb.rotation, 1));
        float3 uz = normalize(GetColumn(obb.rotation, 2));
        float3 D = p_pos - obb.center;
        
        float3 p = float3(dot(D, ux), dot(D, uy), dot(D, uz));
        
        if (abs(p.x) <= obb.size.x && abs(p.y) <= obb.size.y && abs(p.z) <= obb.size.z)
        {
            float3 d = float3(abs(p.x) - obb.size.x, abs(p.y) - obb.size.y, abs(p.z) - obb.size.z);
    
            p_col.pos = CollisionPoint(d, p, ux, uy, uz, obb);
            p_col.normal = ComputeNormal(d, p, ux, uy, uz);
            p_col.id = i;
            
            return true;
        }
    }
    return false;
}

[numthreads(SIZE,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    while (id.x < _NumParticles)
    {   
        Collision col;
        ObbsCollided[id.x] = 0;
        bool isCollided = OBBCollision(Positions[id.x], col);
        if (isCollided) // Collision detected
        {
            SplashPos[id.x] = float4(col.pos, 1.);
            SplashNormal[id.x] = col.normal;
            ObbsCollided[id.x] = col.id;
            
            changePosVel(id, float3(Positions[id.x].x, _Max.y, Positions[id.x].z));
        }
        // Check collision with rain box
        else if (Positions[id.x].y < _Min.y) // Collision with the bottom of the rain box
        {
            // Add splatch on particle collided rain box
            SplashPos[id.x] = float4(Positions[id.x].x, _Min.y, Positions[id.x].z, 1.);
            SplashNormal[id.x] = float3(0., 1., 0.);
            
            float dist = Positions[id.x].y - _Min.y;
            changePosVel(id, float3(Positions[id.x].x, _Max.y + dist, Positions[id.x].z));
        }
        else if (Positions[id.x].x < _Min.x)
        {
            float dist = _Min.x - Positions[id.x].x;
            changePosVel(id, float3(_Max.x - dist, Positions[id.x].y, Positions[id.x].z));
        }
        else if (Positions[id.x].x > _Max.x)
        {
            float dist = Positions[id.x].x - _Max.x;
            changePosVel(id, float3(_Min.x + dist, Positions[id.x].y, Positions[id.x].z));
        }
        else if (Positions[id.x].z < _Min.z)
        {
            float dist = Positions[id.x].z - _Min.z;
            changePosVel(id, float3(Positions[id.x].x, Positions[id.x].y, _Max.z - dist));
        }
        else if (Positions[id.x].z > _Max.z)
        {
            float dist = Positions[id.x].z - _Max.z;
            changePosVel(id, float3(Positions[id.x].x, Positions[id.x].y, _Min.z + dist));
        }
        id.x += SIZE * _Resolution;
    }
}
